CCEffect %{
  techniques:
  - passes:
    - vert: vs
      frag: fs
      blendState:
        targets:
        - blend: true
      rasterizerState:
        cullMode: none
      properties:
        texture: { value: white }
        alphaThreshold: { value: 0.5 }
        size: { value: [500.0, 500.0], editor: { tooltip: '节点尺寸' } }
}%


CCProgram vs %{
  precision highp float;

  #include <cc-global>
  #include <cc-local>

  in vec3 a_position;
  in vec4 a_color;
  out vec4 v_color;

  #if USE_TEXTURE
  in vec2 a_uv0;
  out vec2 v_uv0;
  #endif
 
  void main () {
    vec4 pos = vec4(a_position, 1);

    #if CC_USE_MODEL
    pos = cc_matViewProj * cc_matWorld * pos;
    #else
    pos = cc_matViewProj * pos;
    #endif

    #if USE_TEXTURE
    v_uv0 = a_uv0;
    #endif

    v_color = a_color;

    gl_Position = pos;
  }
}%


CCProgram fs %{
  precision highp float;
  
  #include <alpha-test>
  #include <texture>

  in vec4 v_color;

  #if USE_TEXTURE
  in vec2 v_uv0;
  uniform sampler2D texture;
  #endif

  uniform Properties {
    vec2 size;
  };
  
  // 模糊半径
  // for 循环的次数必须为常量
  const float RADIUS = 20.0;

  // 获取模糊颜色
  vec4 getBlurColor (vec2 pos) {
    vec4 color = vec4(0); // 初始颜色
    float sum = 0.0; // 总权重
    // 卷积过程
    for (float r = -RADIUS; r <= RADIUS; r++) { // 水平方向
      for (float c = -RADIUS; c <= RADIUS; c++) { // 垂直方向
        vec2 target = pos + vec2(r / size.x, c / size.y); // 目标像素位置
        float weight = (RADIUS - abs(r)) * (RADIUS - abs(c)); // 计算权重
        color += texture2D(texture, target) * weight; // 累加颜色
        sum += weight; // 累加权重
      }
    }
    color /= sum; // 求出平均值
    return color;
  }
  vec4 blur13(sampler2D image, vec2 uv) {
  vec4 color = vec4(0.0);
  vec2 off1 = vec2(1.411764705882353);
  vec2 off2 = vec2(3.2941176470588234);
  vec2 off3 = vec2(5.176470588235294);
  color += texture2D(image, uv) * 0.1964825501511404;
  color += texture2D(image, uv + (off1 / size)) * 0.2969069646728344;
  color += texture2D(image, uv - (off1 / size)) * 0.2969069646728344;
  color += texture2D(image, uv + (off2 / size)) * 0.09447039785044732;
  color += texture2D(image, uv - (off2 / size)) * 0.09447039785044732;
  color += texture2D(image, uv + (off3 / size)) * 0.010381362401148057;
  color += texture2D(image, uv - (off3 / size)) * 0.010381362401148057;
  return color;
}
  void main () {
    vec4 color = v_color*blur13(texture,v_uv0); // 获取模糊后的颜色
    color.a = v_color.a; // 还原透明度
    ALPHA_TEST(color);
    gl_FragColor = color;
  }
}%